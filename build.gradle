// plugin dependencies
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-clover-plugin:2.2.1'
        classpath 'de.dynamicfiles.projects.gradle.plugins:javafx-gradle-plugin:8.8.2'
    }
}

// plugins
plugins {
    id 'java'
    id 'groovy'
    id 'org.sonarqube' version '2.6'
}

// static code analysis
apply plugin: 'com.bmuschko.clover'
// javafx packaging
apply plugin: 'javafx-gradle-plugin'

// general project settings
group 'com,github.oczarnecki'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

// project repositories
repositories {
    mavenCentral()
}

// project dependencies
dependencies {
    // json api and implementation
    implementation 'javax.json:javax.json-api:1.0'
    implementation 'org.glassfish:javax.json:1.1.3'


    // spock framework and dependencies
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.3'
    testImplementation 'cglib:cglib-nodep:3.2.9'
    testImplementation 'org.objenesis:objenesis:3.0.1'
    // automatic equals and hashCode verification
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.0'
    // TestFX for GUI testing
    testImplementation 'org.testfx:testfx-core:4.0.13-alpha'
    testImplementation 'org.testfx:testfx-spock:4.0.13-alpha'


    // clover static code analysis
    clover 'org.openclover:clover:4.2.0'
}

// configuration of the jar file
jar {
    manifest {
        attributes(
                'Main-Class': 'com.github.oczarnecki.messageevaluator.MessageEvaluator'
        )
    }

    // package all dependencies into the jar file
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// java configuration
jfx {
    // minimal requirement for jfxJar-task
    mainClass = 'com.github.oczarnecki.messageevaluator.MessageEvaluator'

    // minimal requirement for jfxNative-task
    vendor = 'OCzarnecki'
}

// generate the clover report only in html format
report {
    xml = false
    html = true
}
